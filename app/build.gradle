apply plugin: 'com.android.application'
import java.util.regex.Pattern

android {
    compileSdk = 36
    defaultConfig {
        applicationId = 'de.hosenhasser.funktrainer'
        minSdkVersion 21
        targetSdk = 36
        vectorDrawables.useSupportLibrary = true
        // versionCode getVersionCodeFromManifest()
        versionCode = 54
        versionName = '1.4.1.0'
    }
    buildTypes {
        debug {
            minifyEnabled = false
        }
        release {
            minifyEnabled = false
            shrinkResources = false
            proguardFiles = [getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt']
        }
    }
    compileOptions {
        targetCompatibility = 17
        sourceCompatibility = 17
    }

    splits {
        abi {
            enable = true // enable ABI split feature to create one APK per ABI
            universalApk = true //generate an additional APK that targets all the ABIs
        }
    }
    lint {
        disable = ['PluralsCandidate', 'UnusedAttribute', 'UnusedResources', 'IconDipSize', 'IconLocation', 'IconDuplicates', 'IconMissingDensityFolder']
    }
    namespace = 'de.hosenhasser.funktrainer'

    //versionCode digit for each supported ABI, with 64bit>32bit and x86>armeabi-*
    project.ext.versionCodes = ['armeabi': 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, 'mips': 5, 'mips64': 6, 'x86': 8, 'x86_64': 9]

    android.applicationVariants.all { variant ->
        // assign different version code for each output
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) * 1000000 + defaultConfig.versionCode
        }
    }
}

dependencies {
     //implementation 'com.github.barteksc:android-pdf-viewer:2.8.2'
    // switch back to above as soon as original author moves to mavencentral
    implementation 'com.github.mhiew:android-pdf-viewer:3.2.0-beta.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'com.google.guava:guava:33.4.8-jre'
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.19.0'
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:2.1.20") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.1.20") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
        // constraints {
        //        implementation(project(":kotlin-sdklib-jdk7"))
        //        implementation(project(":kotlin-sdklib-jdk8"))
        // }
    }
}

def getVersionCodeFromManifest() {
    def manifestFile = file(android.sourceSets.main.manifest.srcFile)
    def pattern = Pattern.compile("versionCode=\"(\\d+)\"")
    def matcher = pattern.matcher(manifestFile.getText())
    matcher.find()
    return Integer.parseInt(matcher.group(1))
}